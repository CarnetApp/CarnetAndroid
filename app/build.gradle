apply plugin: 'com.android.application'

static def getDate() {
    // we increment version number every 1000 seconds,
    // we start on 31/08/2016
    def date = new Date().getTime() / 1000L - 1472637600L
    return (int) (date / 1000L)
}

static def getFormattedDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd.HHmm')
    return formattedDate
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    dexOptions {
        javaMaxHeapSize "4g"
    }
    defaultConfig {
        applicationId "com.spisoft.quicknote"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode getDate()
        versionName '0.1-' + getFormattedDate()
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            resValue "string", "app_name", "Carnet"
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "Carnet debug"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    flavorDimensions "version"
    productFlavors {
        fdroid {
            dimension "version"
            versionCode 128
            versionName '0.12.8'
        }
        google {
            dimension "version"
        }
    }
}

if (file("../../Internal").exists()) {
    apply from: '../../Internal/internal_build.gradle'
}

dependencies {
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jsoup:jsoup:1.8.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android:flexbox:0.3.1'
    implementation project(':Sync')
    googleImplementation 'com.google.android.gms:play-services-drive:16.0.0'
    googleImplementation 'com.google.android.gms:play-services-auth:16.0.1'
    googleImplementation project(':GoogleSync')
}
