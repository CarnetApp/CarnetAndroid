apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

static def getDate() {
    // we increment version number every 1000 seconds,
    // we start on 31/08/2016
    def date = new Date().getTime() / 1000L - 1472637600L
    return (int) (date / 1000L)
}
version = "0.22.1"
static def getFormattedDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd.HHmm')
    return formattedDate
}

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'
    dexOptions {
        javaMaxHeapSize "4g"
    }
    defaultConfig {
        applicationId "com.spisoft.quicknote"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode getDate()
        versionName version+"-" + getFormattedDate()
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            resValue "string", "app_name", "Carnet"
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "Carnet debug"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "version"
    productFlavors {
        fdroid {
            dimension "version"
            versionCode 221
            versionName "0.22.1"
        }
        google {
            dimension "version"
        }
    }
}

if (file("../../Internal").exists()) {
    apply from: '../../Internal/internal_build.gradle'
}

dependencies {
    implementation 'com.github.ACRA.acra:acra-dialog:acra-5.2.1'
    implementation 'com.github.ACRA.acra:acra-mail:acra-5.2.1'
    implementation 'com.github.ACRA.acra:acra-notification:acra-5.2.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.preference:preference:1.0.0"
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'com.google.android:flexbox:0.3.1'
    implementation project(':Sync')
    compile 'top.oply.opuslib:opuslib:1.0.2'
   // implementation project(':OpusAndroid')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    googleImplementation 'com.google.android.gms:play-services-drive:16.0.0'
    googleImplementation 'com.google.android.gms:play-services-auth:16.0.1'
    googleImplementation project(':GoogleSync')
    implementation 'com.github.google:cameraview:3eaeac09a7f'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.squti:Android-Wave-Recorder:1.4.0'
}
